# UNMAINTAINED.
# Contributor: Hal Gentz <zegentzy@protonmail.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: Rafał Kozdrój <kozeid2@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=libglvnd-git-gentz
pkgver=1.1.1.r17.gbe09e1e
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
groups=('gentz_custom')
# libglvnd needs mesa for indirect rendering
depends=('libxext' 'mesa' 'opengl-driver')
makedepends=('libxext' 'libx11' 'glproto' 'python' 'git')
provides=('libgl' 'libegl' 'libgles' "libglvnd=${pkgver}" "libglvnd-git=${pkgver}")
conflicts=("libglvnd")
source=(
    "${pkgname}::git+https://github.com/NVIDIA/libglvnd.git"
    LICENSE
)
sha512sums=('SKIP'
            'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5')

pkgver() {
	cd "${srcdir}/${pkgname}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}/${pkgname}"
    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
	cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install

    # NVIDIA is incompetent.
    # They couldn't make a package that doesn't conflict with mesa if 
    # their lives fucking depended on it.
    rm "${pkgdir}/usr/include/EGL" -rf
    rm "${pkgdir}/usr/include/GL" -rf
    rm "${pkgdir}/usr/include/GLES" -rf
    rm "${pkgdir}/usr/include/GLES2" -rf
    rm "${pkgdir}/usr/include/GLES3" -rf
    rm "${pkgdir}/usr/include/KHR" -rf

    install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
