# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Bash Booster <Booster.sdk@gmail.com>
# Contributor: Riccardo Berto <riccardobrt@gmail.com>
# Contributor: vbmithr
# Contributor: Flamelab <panosfilip at gmail dot com>
# Contributor: Dan Ziemba <zman0900@gmail.com>

_realpkgname=xf86-video-ati
pkgname=xf86-video-ati-git-gentz
pkgver=19.1.0.r0.gb9bd8097
pkgrel=4
pkgdesc='X.org ati video driver. Git version'
arch=('x86_64')
url='http://xorg.freedesktop.org/'
license=('custom')
groups=('xorg-drivers' 'gentz_custom')
depends=('libdrm' 'systemd' 'mesa')
makedepends=('xorg-server-devel' 'git' 'systemd' 'xorgproto')
provides=('xf86-video-ati' 'xf86-video-ati-git')
conflicts=('xf86-video-ati')
source=('git://anongit.freedesktop.org/xorg/driver/xf86-video-ati')
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/xf86-video-ati"
    ( set -o pipefail
        git describe --long \
            | sed 's/^xf86-video-ati-//;s/\([^-]*-g\)/r\1/;s/-/./g'
    )
}

_exports() {
    # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
    # With them, module fail to load with undefined symbol.
    #
    # See https://bugs.archlinux.org/task/55102
    # and https://bugs.archlinux.org/task/54845
    export CFLAGS=${CFLAGS/-fno-plt}
    export CXXFLAGS=${CXXFLAGS/-fno-plt}
    export LDFLAGS=${LDFLAGS/,-z,now}

    export CFLAGS="${CFLAGS} -fplt -fno-lto -g"
    export CXXFLAGS="${CXXFLAGS} -fplt -fno-lto -g"
    export LDFLAGS="${LDFLAGS},-fno-lto"
}

build() {
    cd "${srcdir}/xf86-video-ati"

    _exports
    ./autogen.sh --prefix=/usr --enable-glamor
    make
}

package() {
    cd "${srcdir}/xf86-video-ati"

    _exports
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
