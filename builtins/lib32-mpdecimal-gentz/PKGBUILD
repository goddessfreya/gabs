# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Andrew Sun <adsun701@gmail.com>

pkgname=lib32-mpdecimal-gentz
pkgver=2.4.2
pkgrel=34
pkgdesc='Package for correctly-rounded arbitrary precision decimal floating point arithmetic (32-bit)'
arch=('x86_64')
url='https://www.bytereef.org/mpdecimal/index.html'
license=('custom')
groups=('gentz_custom')
depends=('lib32-glibc' 'lib32-gcc-libs' 'mpdecimal')
provides=('lib32-mpdecimal')
source=("https://www.bytereef.org/software/mpdecimal/releases/mpdecimal-${pkgver}.tar.gz")
sha512sums=('eb18ad53b81b93c469db4d915bbb3fae21b36ad82a88e01fef3a6946ac5f50b54376e259a5ecbe23836f1efd59d226b942ecdee87eaba7f9e75cdcaaa9499ef7')

prepare() {
    cd "${srcdir}/mpdecimal-${pkgver}"
    autoreconf -fiv
}

_exports_32() {
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    export CFLAGS="${CFLAGS} -m32"
    export CXXFLAGS="${CXXFLAGS} -m32"
    export LDFLAGS="${LDFLAGS} -m32"
}

build() {
    _exports_32
    cd "${srcdir}/mpdecimal-${pkgver}"

    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32
    make
}

package() {
    cd "${srcdir}/mpdecimal-${pkgver}"
    _exports_32
    make DESTDIR="${pkgdir}" install
    rm -rf "${pkgdir}/usr/"{bin,include,share}
    install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
