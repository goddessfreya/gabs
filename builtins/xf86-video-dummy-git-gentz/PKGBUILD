# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Yurii Kolesnykov <root@yurikoles.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=xf86-video-dummy
pkgname=xf86-video-dummy-git-gentz
pkgver=19.1.0.0
pkgrel=7
pkgdesc='X.org dummy video driver (git version)'
arch=('x86_64')
url='https://xorg.freedesktop.org/'
license=('custom')
groups=('xorg-drivers' 'gentz_custom')
depends=('mesa')
makedepends=('xorg-server-devel' 'git' 'pixman' 'xorgproto')
provides=('xf86-video-dummy' 'xf86-video-dummy-git')
conflicts=('xf86-video-dummy' 'xorg-server<1.20.0')
source=("${pkgname}::git+https://gitlab.freedesktop.org/xorg/driver/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
    local version count

    cd "${srcdir}/${pkgname}"

    version="$(git describe --abbrev=0 --tags)"
    count="$(git rev-list --count ${version}..)"
    printf '%s.%s' "${version#v}" "${count}" | sed 's/^xf86-video-dummy-//'
}

_exports() {
    # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
    # With them, module fail to load with undefined symbol.
    #
    # See https://bugs.archlinux.org/task/55102
    # and https://bugs.archlinux.org/task/54845
    export CFLAGS=${CFLAGS/-fno-plt}
    export CXXFLAGS=${CXXFLAGS/-fno-plt}
    export LDFLAGS=${LDFLAGS/,-z,now}

    export CFLAGS="${CFLAGS} -fplt -fno-lto -g"
    export CXXFLAGS="${CXXFLAGS} -fplt -fno-lto -g"
    export LDFLAGS="${LDFLAGS},-fno-lto"
}

build() {
    cd "${srcdir}/${pkgname}"

    _exports
    ./autogen.sh --prefix=/usr  
    make
}

package() {
    cd "${srcdir}/${pkgname}"

    _exports
    make "DESTDIR=${pkgdir}" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
