# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=openmpi-git-gentz
pkgver=4.1.0.a1.r30143.raa8be9c12d
pkgrel=1
pkgdesc='High performance message passing library (MPI)'
arch=('x86_64')
url='https://www.open-mpi.org'
license=('custom:OpenMPI')
groups=('gentz_custom')
depends=('libltdl' 'hwloc' 'openssh' 'zlib' 'libnl')
makedepends=('inetutils' 'valgrind' 'gcc-fortran' 'git')
optdepends=('gcc-fortran: fortran support')
provides=('openmpi' 'openmpi-git')
conflicts=('openmpi' 'openmpi-git')
options=('staticlibs')
source=('git+https://github.com/open-mpi/ompi.git')
sha512sums=('SKIP')

pkgver() {
	cd "${srcdir}/ompi"
	. VERSION
	printf "$major.$minor.$release.$greek.r%s.r%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/ompi"
    ./autogen.pl
    ./configure --prefix=/usr \
        --sysconfdir="/etc/openmpi" \
        --enable-mpi-fortran=all \
        --libdir="/usr/lib/openmpi" \
        --with-threads=posix \
        --enable-mpi-thread-multiple \
        --enable-smp-locks \
        --enable-builtin-atomics \
        --enable-mpi-cxx \
        --with-valgrind \
        --enable-memchecker \
        --enable-mpi1-compatibility \
        --enable-pretty-print-stacktrace \
        --without-slurm \
        --with-hwloc=/usr \
        --with-libltdl=/usr \
        FC="$(which gfortran)" \
        LDFLAGS="${LDFLAGS} -Wl,-z,noexecstack"
}

build() {
	cd "${srcdir}/ompi"
    make
}

check() {
	cd "${srcdir}/ompi"
    make check
}

package() {
	cd "${srcdir}/ompi"
    make DESTDIR="${pkgdir}" install

    # FS#28583
    install -dm755 "${pkgdir}/usr/lib/pkgconfig"
    for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
        ln -sf "/usr/lib/openmpi/pkgconfig/${i}" "${pkgdir}/usr/lib/pkgconfig/"
    done

    install -dm755 "${pkgdir}/etc/ld.so.conf.d"
    echo "/usr/lib/openmpi" > "${pkgdir}/etc/ld.so.conf.d/openmpi.conf"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
