# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgbase=python2-gentz
pkgname=('python2-gentz' 'lib32-python2-gentz')
pkgver=2.7.17
pkgrel=39
pkgdesc='A high-level scripting language'
arch=('x86_64')
url='https://www.python.org/'
license=('PSF')
groups=('gentz_custom')
makedepends=(
    'tk' 'bluez-libs' 'lib32-gcc-libs' 'lib32-tk' 'gdb' 'file'
)
source=(
    "https://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz"{,.asc}
    'pyconfig.h'
    'lib32-distutils-sysconfig.patch'
    'python-config-32.patch'
)
sha512sums=('2dc19a0b0d818c71429dae94783e58b2aac0fa31f5faa1e840cac06245a59932ecc4658d913515736601bcf70a78c9ec60367aed75f4567d1e41ff3bb104da9a'
            'SKIP'
            'e0df774b373178632963c5744eeeff7e2b460e28c2208c8a7f6ddda7f316d5b2d12576bec52a41a3eed0636339266121a4bb068d4a00ed0050bbd5f9cbf800db'
            '3bda8aa91a8cddee7f15e42cf4e0d96555aaa2ef21a2b2a9bedd907a24ba3242d1a681229e99a81f5501abad4e6250f36d3fb105d3f4a62b79596599a959a530'
            '9f2ff9b9da9c326f9c9f97cc393ccac8ca1be170243882ca6b463238b078372394d30e1f9702b1a6906afc8884859b3fb0d10f54230a010adc4834b475c8608c')

# Benjamin Peterson
validpgpkeys=('C01E1CAD5EA2C4F0B8E3571504C367C218ADD4FF')

_pybasever=${pkgver%.*}

prepare() {
    [[ "$(pacman -S --print-format "%v" python2 | rev | cut -d"-" -f2- | rev)" != "${pkgver}" ]] && exit 1

    mv "${srcdir}/Python-${pkgver}" "${srcdir}/${pkgbase}64"
    pushd "${srcdir}/${pkgbase}64"
    # Temporary workaround for FS#22322
    # See http://bugs.python.org/issue10835 for upstream report
    sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

    # FS#23997
    sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

    # Ensure that we are using the system copy of various libraries
    # (expat, zlib and libffi), rather than copies shipped in the tarball
    rm -r Modules/expat
    rm -r Modules/zlib
    rm -r Modules/_ctypes/{darwin,libffi}*

    # clean up shebangs
    find . -name '*.py' \
        | xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

    # Workaround asdl_c.py/makeopcodetargets.py errors after we touched the
    # shebangs
    touch Include/Python-ast.h Python/Python-ast.c Python/opcode_targets.h

    popd

    cp -r "${srcdir}/${pkgbase}"{64,32}

    pushd "${srcdir}/${pkgbase}32"
    # Fix hard-coded paths
    sed -i "s|base}/lib|base}/lib32|g" "Lib/sysconfig.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" \
        "Lib/sysconfig.py"
    sed -i "s|lib/|lib32/|g" "Modules/getpath.c"
    sed -i "s|base/lib|base/lib32|g" "Lib/distutils/command/install.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" \
        "Lib/distutils/command/install.py"
    sed -i "s|prefix)/lib|prefix)/lib32|g" "Makefile.pre.in"

    patch -Np1 -i "../lib32-distutils-sysconfig.patch"

    # Give the configuration script an extention
    patch -Np1 -i "../python-config-32.patch"
    popd

    _configure_python "32" \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32 \
        --includedir="/usr/lib32/python${_pybasever}/include" \
        --exec_prefix="/usr/lib32/python${_pybasever}/" \
        --bindir=/usr/bin \
        --sbindir=/usr/bin \
        --with-suffix='-32'

    _configure_python "64"
}

_exports_32() {
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    export CFLAGS="${CFLAGS} -m32"
    #export CXXFLAGS="${CXXFLAGS} -m32"
    export LDFLAGS="${LDFLAGS} -m32"
    _export_O3
}

_exports_64() {
    export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
    export CFLAGS="${CFLAGS/-m32}"
    #export CXXFLAGS="${CXXFLAGS/-m32}"
    export LDFLAGS="${LDFLAGS/-m32}"
    _export_O3
}

_export_O3() {
    # PGO should be done with -O3
    export CFLAGS="${CFLAGS/-O0}"
    export CFLAGS="${CFLAGS/-Og}"
    export CFLAGS="${CFLAGS/-O1}"
    export CFLAGS="${CFLAGS/-O2}"
    export CFLAGS="${CFLAGS/-O3} -O3"
}

_configure_python() {
    _exports_${1}
    pushd "${srcdir}/${pkgbase}${1}"

    local _python_configure_options=(
        --prefix=/usr
        --enable-shared
        --with-threads
        --enable-optimizations
        --with-lto
        --enable-ipv6
        --enable-unicode=ucs4
        --with-system-expat
        --with-dbmliborder=gdbm:ndbm
        --with-system-ffi
        --without-ensurepip
    )

    # Disable bundled pip & setuptools
    ./configure \
        "${_python_configure_options[@]}" \
        "${@:2}"

    popd
}

_build_python() {
    _exports_${1}
    pushd "${srcdir}/${pkgbase}${1}"

    OPT="${CFLAGS}" make

    popd
}

build() {
    _build_python "32"
    _build_python "64"
}

_package_common() {
    _exports_${1}
    cd "${srcdir}/${pkgbase}${1}"

    # Hack to avoid building again
    sed -i 's/^all:.*$/all: build_all/' Makefile

    make DESTDIR="${pkgdir}" altinstall maninstall

    # Provided by python
    rm "${pkgdir}/usr/share/man/man1/python.1"

    # Why are these not done by default...
    ln -s "python${_pybasever}${2}" "${pkgdir}/usr/bin/python2${2}"
    ln -s "python${_pybasever}${2}-config" "${pkgdir}/usr/bin/python2${2}-config"

    # FS#33954
    ln -sf "python-${_pybasever}.pc" \
        "${pkgdir}/usr/lib${3}/pkgconfig/python2.pc"

    ln -sf "../../libpython${_pybasever}.so" \
        "${pkgdir}/usr/lib${3}/python${_pybasever}/config/libpython${_pybasever}.so"

    mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/lib${3}/python${_pybasever}/"

    # some useful "stuff"
    install -dm755 "${pkgdir}/usr/lib${3}/python${_pybasever}/Tools/"{i18n,scripts}
    install -m755 Tools/i18n/{msgfmt,pygettext}.py \
        "${pkgdir}/usr/lib${3}/python${_pybasever}/Tools/i18n/"
    install -m755 Tools/scripts/{README,*py} \
        "${pkgdir}/usr/lib${3}/python${_pybasever}/Tools/scripts/"

    # clean-up reference to build directory
    sed -i "s#${srcdir}/${pkgbase}${1}:##" \
        "${pkgdir}/usr/lib${3}/python${_pybasever}/config/Makefile"

    # license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-gentz() {
    depends=('bzip2' 'expat' 'gdbm' 'libffi' 'libnsl' 'openssl' 'sqlite' 'zlib')
    optdepends=('tk: for IDLE'
                'python2-setuptools'
                'python2-pip')
    provides=("python2=${pkgver}")
    conflicts=('python<3' 'python2')

    _package_common "64"

    # Why are these not done by default...
    ln -sf "python${_pybasever}.1" "${pkgdir}/usr/share/man/man1/python2.1"

    # fix conflicts with python
    mv "${pkgdir}/usr/bin/idle"{,2}
    mv "${pkgdir}/usr/bin/pydoc"{,2}
    mv "${pkgdir}/usr/bin/2to3"{,"-${_pybasever}"}

    # pyconfs
    mv "${pkgdir}/usr/include/python${_pybasever}/pyconfig"{,-64}.h
    install -Dm644 ../pyconfig.h \
        "${pkgdir}/usr/include/python${_pybasever}/pyconfig.h"
}

package_lib32-python2-gentz() {
    pkgdesc+=" (32-bit)"
    depends=(
        'python2'
        'lib32-expat'
        'lib32-bzip2'
        'gdbm'
        'lib32-openssl'
        'lib32-sqlite'
        'lib32-libffi'
        'lib32-zlib'
        'lib32-libnsl'
    )
    optdepends=(
        'python-setuptools'
        'python-pip'
        'lib32-tk: for tkinter'
    )
    provides=("lib32-python2=${pkgver}")
    conflicts=('lib32-python<3' 'lib32-python2')

    _package_common "32" "-32" "32"

    rm -rf "${pkgdir}/"{etc,usr/{share,include}}
    rm "${pkgdir}/usr/bin/"{pydoc*,idle*,2to3*}

    # pyconfs
    install -Dm644 pyconfig.h \
        "${pkgdir}/usr/include/python${_pybasever}/pyconfig-32.h"

    # clean up shebangs
    find "${pkgdir}/usr/lib32/python${_pybasever}/" -name '*.py' \
        | xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python${_pybasever}-32|"
}
