# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-server-git-gentz
pkgver=1.20.0.r485.ga1e3dfa50
pkgrel=2
pkgdesc='Xorg server, common and dev files, and other applications and utils'
arch=('x86_64')
url='http://xorg.freedesktop.org'
license=('custom')
groups=('xorg' 'gentz_custom')
makedepends=(
    'xorgproto' 'pixman' 'libx11' 'mesa' 'mesa-libgl' 'xtrans' 'libxkbfile'
    'libxfont2' 'libpciaccess' 'libxv' 'libxmu' 'libxrender' 'libxi' 'libxaw'
    'libxtst' 'libxres' 'xorg-xkbcomp' 'xorg-util-macros'
    'xorg-font-util' 'libepoxy' 'xcb-util' 'xcb-util-image'
    'xcb-util-renderutil' 'xcb-util-wm' 'xcb-util-keysyms' 'libxshmfence'
    'libunwind' 'systemd' 'wayland-protocols' 'egl-wayland' 'meson' 'git'
    'libepoxy' 'lib32-libepoxy'
)
# FS#52949
depends=(
    'dbus' 'xf86-input-libinput' 'xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap'
    'xorg-util-macros' 'xorg-server-common' 'xorg-xauth' 'xcb-util-image'
    'xcb-util-renderutil' 'xcb-util-wm' 'xorgproto' 'mesa' 'libpciaccess'
    'xcb-util-keysyms' 'systemd-libs' 'pixman' 'nettle' 'libepoxy' 'libxfont2'
    'libunwind' 'libgl' 'libpciaccess' 'libdrm' 'libxshmfence' 'libxv' 'libtirpc'
    'libxext' 'libxi' 'libxaw' 'libxrender' 'libxfixes' 'libbsd'
)
# see xorg-server-*/hw/xfree86/common/xf86Module.h for ABI versions -
# we provide major numbers that drivers can depend on
# and /usr/lib/pkgconfig/xorg-server.pc in xorg-server-devel pkg
_VIDEODRV_version=25.1
_XINPUT_version=24.1
_EXTENSION_version=10.0
provides=(
    'xorg-server-common' 'xorg-server-common-git' 'xorg-server-git'
    "X-ABI-VIDEODRV_VERSION=${_VIDEODRV_version}" 
    "X-ABI-XINPUT_VERSION=${_XINPUT_version}" 
    "X-ABI-EXTENSION_VERSION=${_EXTENSION_version}" 'x-server' 'xorg-server' 
    'xorg-server-xephyr-git' 'xorg-server-xvfb' 'xorg-server-xvfb-git'
    'xorg-server-xnest' 'xorg-server-xnest-git'    'xorg-server-xwayland' 
    'xorg-server-xwayland-git' 'xorg-server-devel' 'xorg-server-devel-git'
    'xorg-server-xephyr'
)
conflicts=(
    'xorg-server-common' 'xorg-server' 'xorg-server-xephyr' 'xorg-server-xvfb'
    'nvidia-utils<=331.20' 'glamor-egl' 'xf86-video-modesetting'
    'xorg-server-xnest' 'xorg-server-xwayland' 'xorg-server-devel'
)
replaces=(
    'glamor-egl' 'xf86-video-modesetting' 'xorg-server-common-git-gentz'
    'xorg-server-xephyr-git-gentz' 'xorg-server-xvfb-git-gentz'
    'xorg-server-xnest-git-gentz' 'xorg-server-xwayland-git-gentz' 
    'xorg-server-devel-git-gentz'
)
source=(
    'git://anongit.freedesktop.org/xorg/xserver'
    'xvfb-run' # with updates from FC master
    'xvfb-run.1'
    '367.patch'
)
validpgpkeys=(
    '7B27A3F1A6E18CD9588B4AE8310180050905E40C'
    'C383B778255613DFDB409D91DB221A6900000011'
    'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3'
    '995ED5C8A6138EB0961F18474C09DD83CAAA50B2'
)
sha512sums=('SKIP'
            '55bbf520333f6e818b0125b37179a7039b69a0d3d2242b80a08da003d94cbf6c1fb912d880abcce318a85d7947e3eff8fbc4cdf57d7118572e8ebc56c4569af6'
            'de5e2cb3c6825e6cf1f07ca0d52423e17f34d70ec7935e9dd24be5fb9883bf1e03b50ff584931bd3b41095c510ab2aa44d2573fd5feaebdcb59363b65607ff22'
            'c7ddf579aa08f5b195b181dd01325e1cfc54a9a3e7399e1b829ff98196a3cb97b8d38cea298022218753df855d41299fb239721466ab24519db2821c2e1d6372')
install=xorg-server.install

_meson_flags=(-D b_ndebug=false)

pkgver() {
    cd "${srcdir}/xserver"

    git describe --long | cut -d "-" -f3-5 | sed "s/-/.r/" | tr - .
}

_exports() {
    # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
    # With them, module fail to load with undefined symbol.
    #
    # See https://bugs.archlinux.org/task/55102
    # and https://bugs.archlinux.org/task/54845
    export CFLAGS=${CFLAGS/-fno-plt}
    export CXXFLAGS=${CXXFLAGS/-fno-plt}
    export LDFLAGS=${LDFLAGS/,-z,now}

    export CFLAGS="${CFLAGS} -fplt -fno-lto -g3"
    export CXXFLAGS="${CXXFLAGS} -fplt -fno-lto -g3"
    export LDFLAGS="${LDFLAGS},-fno-lto"

    # For dbg symbols, afaik.
    #export CFLAGS="${CFLAGS} -O0"
    #export CXXFLAGS="${CXXFLAGS} -O0"
    #export CFLAGS=${CFLAGS/-O3}
    #export CXXFLAGS=${CXXFLAGS/-O3}
}

prepare() {
    _exports

    local _versions_dont_match=false
    for VAR in VIDEODRV XINPUT EXTENSION; do
        local _current_version="$(eval "echo \$_${VAR}_version")"
        local _found_version="$(
            grep -Po "${VAR}_V.*\(\K[^)]*" \
                "${srcdir}/xserver/hw/xfree86/common/xf86Module.h" \
                |& sed 's/, /./')"
        if [[ "${_current_version}" != "${_found_version}" ]]; then
            msg "${VAR}: Cur != Found: ${_current_version} != ${_found_version}"
            _versions_dont_match=true
        fi
    done

    if [[ "${_versions_dont_match}" == "true" ]]; then
        exit 1
    fi

    pushd "${srcdir}/xserver"
    # xwayland refactor broke builds
    patch -p1 < ../367.patch
    popd

    arch-meson "${srcdir}/xserver" "${srcdir}/build" \
        "${_meson_flags[@]}" \
        -D os_vendor="Arch Linux" \
        -D ipv6=true \
        -D xvfb=true \
        -D xnest=true \
        -D xcsecurity=true \
        -D xorg=true \
        -D xephyr=true \
        -D xwayland=true \
        -D xwayland_eglstream=true \
        -D glamor=true \
        -D udev=true \
        -D systemd_logind=true \
        -D suid_wrapper=true \
        -D xkb_dir=/usr/share/X11/xkb \
        -D xkb_output_dir=/var/lib/xkb

    # Print config
    meson configure "${srcdir}/build"
}

build() {
    _exports
    ninja ${NINJAFLAGS} -C "${srcdir}/build"
}

package() {
    DESTDIR="${pkgdir}" ninja ${NINJAFLAGS} -C "${srcdir}/build" install

    ln -s /usr/bin/Xorg "${pkgdir}/usr/bin/X"

    install -m644 -Dt "${pkgdir}/var/lib/xkb/" \
        "${srcdir}/xserver/xkb/README.compiled"

    # distro specific files must be installed in /usr/share/X11/xorg.conf.d
    install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"

    install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
    install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/" # outda

    # license
    install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" \
        "${srcdir}/xserver/COPYING"
}
