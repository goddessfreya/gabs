# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgbase=python-gentz
pkgname=('python-gentz' 'lib32-python-gentz')
pkgver=3.8.0
pkgrel=21
pkgdesc='Next generation of the python high-level scripting language'
arch=('x86_64')
url='http://www.python.org/'
license=('custom')
groups=('gentz_custom')
makedepends=(
    'tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal' 'llvm' 'gdb'
    'xorg-server-xvfb' 'lib32-gcc-libs' 'lib32-tk' 'lib32-sqlite'
    'lib32-libnsl' 'lib32-libtirpc' 'lib32-mpdecimal' 'expat'
    'lib32-expat' 'libffi' 'lib32-libffi'
)
source=(
    "https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"{,.asc}
    '0001-compileall-Fix-ddir-when-recursing.patch'
    'pyconfig.h'
    'Use-right-mpdecimal.patch'
    'lib32-distutils-sysconfig.patch'
    'python-config-32.patch'
)
sha512sums=('5f9bfcb3acdf592770a9d5abd2c32c68c55a49b92f958ded069e3ef31cf2d415e67112b4f6738fab237dc29e5c622298719946d2e9471e7e78e3a6bdf2fac1d1'
            'SKIP'
            'ebd04c3b6d41321b1f0d439d356e0ce463760db55dc64109854c70d017cf56608aa19de9fc4a21bf840795ff202b4703444f9af8074b661780798c17e03089ff'
            'e0df774b373178632963c5744eeeff7e2b460e28c2208c8a7f6ddda7f316d5b2d12576bec52a41a3eed0636339266121a4bb068d4a00ed0050bbd5f9cbf800db'
            'a8ba987239600e43cdb83bbed9810e8d34b1239784dd6d16cbeecb5a4f92440e3656e0622e756eace2e9d2d7bd5ac8b4593538195308f852a2000f168a2c5d4e'
            'a0f1efad2af493dc67e7a03aea721054c6047bf1edb90960377813523e3f033eddb8b71819c8ee645b2217d6411794bbe3d608da3ecf76ef873a2cb47044c8b3'
            'ab772cda692548c0ab1d7db1b84e0f64cede99b206de36d983db3b9a0b576d5d4201f6ff160dccc4e6ec00073b46621a44946b3cddbda8705357e176508b1980')

validpgpkeys=('0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D'  # Ned Deily (Python release signing key) <nad@python.org>
              'E3FF2839C048B25C084DEBE9B26995E310250568') # Łukasz Langa (GPG langa.pl) <lukasz@langa.pl>

_pybasever=${pkgver%.*}

prepare() {
    [[ "$(pacman -S --print-format "%v" python | rev | cut -d"-" -f2- | rev)" != "${pkgver}" ]] && exit 1

    mv "${srcdir}/Python-${pkgver}" "${srcdir}/${pkgbase}64"
    pushd "${srcdir}/${pkgbase}64"

    # FS#59997
    patch -Np1 -i ../0001-compileall-Fix-ddir-when-recursing.patch

    # These tests hang when installing. We don't need them.
    rm -rf Lib/test/*

    # FS#23997
    sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

    # Speed up LTO
    sed -i -e "s|-flto |-flto=thin |g" configure configure.ac

    # Ensure that we are using the system copy of various libraries
    # (expat, libffi, and libmpdec), rather than copies shipped in the tarball
    rm -r Modules/expat
    rm -r Modules/_ctypes/{darwin,libffi}*
    rm -r Modules/_decimal/libmpdec
    popd

    cp -r "${srcdir}/${pkgbase}"{64,32}

    pushd "${srcdir}/${pkgbase}32"
    patch -Np1 -i ../Use-right-mpdecimal.patch

    # Fix hard-coded paths
    sed -i "s|base}/lib|base}/lib32|g" "Lib/sysconfig.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" \
        "Lib/sysconfig.py"
    sed -i "s|lib/|lib32/|g" "Modules/getpath.c"
    sed -i "s|base/lib|base/lib32|g" "Lib/distutils/command/install.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" \
        "Lib/distutils/command/install.py"
    sed -i "s|prefix)/lib|prefix)/lib32|g" "Makefile.pre.in"

    patch -Np1 -i "../lib32-distutils-sysconfig.patch"

    # Give the configuration script an extention
    patch -Np1 -i "../python-config-32.patch"

    popd

    _configure_python "32" \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32 \
        --includedir=/usr/lib32/python${_pybasever}/include \
        --exec_prefix=/usr/lib32/python${_pybasever}/ \
        --bindir=/usr/bin \
        --sbindir=/usr/bin \
        --with-suffix='-32'

    _configure_python "64"
}

_exports_32() {
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    export CFLAGS="$CFLAGS -m32"
    #export CXXFLAGS="$CXXFLAGS -m32"
    export LDFLAGS="$LDFLAGS -m32"
    _export_O3
}

_exports_64() {
    export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
    export CFLAGS="${CFLAGS/-m32}"
    #export CXXFLAGS="${CXXFLAGS/-m32}"
    export LDFLAGS="${LDFLAGS/-m32}"
    _export_O3
}

_export_O3() {
    # PGO should be done with -O3
    export CFLAGS="${CFLAGS/-O0}"
    export CFLAGS="${CFLAGS/-Og}"
    export CFLAGS="${CFLAGS/-O1}"
    export CFLAGS="${CFLAGS/-O2}"
    export CFLAGS="${CFLAGS/-O3} -O3"
}

_obtain_server() {
    # Obtain next free server number for xvfb-run; this even works in a chroot
    # environment.
    export servernum=99
    while ! xvfb-run -a -n "$servernum" /bin/true 2>/dev/null; do
        export servernum=$((servernum+1))
    done
}

_configure_python() {
    _exports_$1
    pushd "${srcdir}/${pkgbase}$1"

    local _python_configure_options=(
        --prefix=/usr
        --enable-shared
        --with-threads
        --with-computed-gotos
        --enable-optimizations
        --with-lto
        --enable-ipv6
        --with-system-expat
        --with-dbmliborder=gdbm:ndbm
        --with-system-ffi
        --with-system-libmpdec
        --enable-loadable-sqlite-extensions
        --without-ensurepip
    )

    # Disable bundled pip & setuptools
    ./configure \
        "${_python_configure_options[@]}" \
        "${@:2}"
}

_build_python() {
    exports_$1
    pushd "${srcdir}/${pkgbase}$1"

    _obtain_server

    LC_CTYPE=en_US.UTF-8 xvfb-run \
        -s "-screen 0 1280x720x24 -ac +extension GLX" \
        -a -n "$servernum" make EXTRA_CFLAGS="$CFLAGS"
    popd
}

_build() {
    _build_python "32"
    _build_python "64"
}

_package_common() {
    _exports_$1
    cd "${srcdir}/${pkgbase}$1"

    # Hack to avoid building again
    sed -i 's/^all:.*$/all: build_all/' Makefile

    # PGO should be done with -O3
    export CFLAGS="${CFLAGS/-O2/-O3}"

    make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" install

    # Why are these not done by default...
    ln -s "python3$2" "${pkgdir}/usr/bin/python$2"
    ln -s "python3$2-config" "${pkgdir}/usr/bin/python$2-config"

    # some useful "stuff" FS#46146
    install -dm755 "${pkgdir}/usr/lib$3/python${_pybasever}/Tools/"{i18n,scripts}
    install -m755 Tools/i18n/{msgfmt,pygettext}.py \
        "${pkgdir}/usr/lib$3/python${_pybasever}/Tools/i18n/"
    install -m755 Tools/scripts/{README,*py} \
        "${pkgdir}/usr/lib$3/python${_pybasever}/Tools/scripts/"

    # License
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-gentz() {
    depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib' 'libnsl')
    optdepends=(
        'python-setuptools'
        'python-pip'
        'sqlite'
        'mpdecimal: for decimal'
        'xz: for lzma'
        'tk: for tkinter'
    )
    provides=('python3' "python=$pkgver")
    conflicts=('python3' 'python')
    replaces=('python3')

    _package_common "64"

    # Why are these not done by default...
    ln -s "python${_pybasever}.1" "${pkgdir}/usr/share/man/man1/python.1"
    ln -s idle3 "${pkgdir}/usr/bin/idle"
    ln -s pydoc3 "${pkgdir}/usr/bin/pydoc"

    # pyconfs
    mv "${pkgdir}/usr/include/python${_pybasever}/pyconfig"{,-64}.h
    install -Dm644 ../pyconfig.h \
        "${pkgdir}/usr/include/python${_pybasever}/pyconfig.h"
}

package_lib32-python-gentz() {
    pkgdesc+=" (32-bit)"
    depends=(
        'python'
        'lib32-expat'
        'lib32-bzip2'
        'gdbm'
        'lib32-openssl'
        'lib32-libffi'
        'lib32-zlib'
        'lib32-libnsl'
    )
    optdepends=(
        'python-setuptools'
        'python-pip'
        'lib32-sqlite'
        'lib32-mpdecimal: for decimal'
        'lib32-xz: for lzma'
        'lib32-tk: for tkinter'
    )
    provides=('lib32-python3' "lib32-python=$pkgver")
    conflicts=('lib32-python3' 'lib32-python')
    replaces=('lib32-python3')

    _package_common "32" "-32" "32"

    # clean up shebangs
    find "${pkgdir}/usr/lib32/python${_pybasever}/" -name '*.py' \
        | xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python${_pybasever}-32|"

    # No conflicts
    rm -rf "${pkgdir}/"{etc,usr/{share,include}}
    rm "${pkgdir}/usr/bin/"{pydoc*,idle*,2to3*}

    # Why this is in the wrong place, I don't knpw
    mv "${pkgdir}/usr/lib"{,32}"/python${_pybasever}/config-${_pybasever}-x86_64-linux-gnu"

    # This symlink has the wrong name for some reason
    mv "${pkgdir}/usr/bin/python3"{,-32}"-config"

    # pyconfs
    install -Dm644 pyconfig.h \
        "${pkgdir}/usr/include/python${_pybasever}/pyconfig-32.h"
}
