# Maintainer: Freya Gentz <zegentzy@protonmail.com>
# Contributor: Det <nimetonmaili g-mail>
# Contributer: AndyRTR <andyrtr@archlinux.org>

pkgname=xorgproto-git-gentz
pkgver=2019.2.0.r2629.gf61f9a3
pkgrel=9
pkgdesc='combined X.Org X11 Protocol headers'
arch=('x86_64')
url='https://xorg.freedesktop.org/'
license=('custom')
groups=('gentz_custom')
makedepends=(
    'xorg-util-macros' 'meson' 'xmlto' 'libxslt' 'linuxdoc-tools' 'docbook-sgml'
    'fop' 'git'
)
provides=(
    'bigreqsproto' 'compositeproto' 'damageproto' 'dmxproto' 'dri2proto'
    'dri3proto' 'fixesproto' 'fontsproto' 'glproto' 'inputproto' 'kbproto'
    'presentproto' 'printproto' 'randrproto' 'recordproto' 'renderproto'
    'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xextproto'
    'xf86dgaproto' 'xf86driproto' 'xf86miscproto' 'xf86vidmodeproto'
    'xineramaproto' 'xproto' 'xorgproto' 'xorgproto-git'
)
conflicts=(
    'bigreqsproto' 'compositeproto' 'damageproto' 'dmxproto' 'dri2proto'
    'dri3proto' 'fixesproto' 'fontsproto' 'glproto' 'inputproto' 'kbproto'
    'presentproto' 'printproto' 'randrproto' 'recordproto' 'renderproto'
    'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xextproto'
    'xf86dgaproto' 'xf86driproto' 'xf86miscproto' 'xf86vidmodeproto'
    'xineramaproto' 'xproto' 'xorgproto' 'xorgproto-git'
)
replaces=(
    'bigreqsproto' 'compositeproto' 'damageproto' 'dmxproto' 'dri2proto'
    'dri3proto' 'fixesproto' 'fontsproto' 'glproto' 'inputproto' 'kbproto'
    'presentproto' 'printproto' 'randrproto' 'recordproto' 'renderproto'
    'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xextproto'
    'xf86dgaproto' 'xf86driproto' 'xf86miscproto' 'xf86vidmodeproto'
    'xineramaproto' 'xproto'
)
source=(
    'git://anongit.freedesktop.org/xorg/proto/xorgproto'
)
sha512sums=('SKIP')
validpgpkeys=(
    '995ED5C8A6138EB0961F18474C09DD83CAAA50B2' #  "Adam Jackson <ajax@nwnk.net>"
    'DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3' # "Adam Jackson <ajax@benzedrine.nwnk.net>"
    'C383B778255613DFDB409D91DB221A6900000011' #  "Keith Packard <keithp@keithp.com>"
    '3C2C43D9447D5938EF4551EBE23B7E70B467F0BF' # "Peter Hutterer (Who-T) <office@who-t.net>"
)

pkgver() {
    cd "${srcdir}/xorgproto"
    printf "%s.r%s.%s\n" \
        "$(git describe --long | cut -d "-" -f2-3 | tr - .)" \
        "$(git rev-list HEAD --count)" \
        "$(git describe --long | cut -d "-" -f4)"
}

prepare() {
    mkdir -p "${srcdir}/build"
}

build() {
    arch-meson "${srcdir}/xorgproto" "${srcdir}/build" \
        -D legacy=false

    ninja -C "${srcdir}/build"
}

check() {
    meson test -C "${srcdir}/build"
}

package() {
    cd "${srcdir}"
    DESTDIR="${pkgdir}" ninja -C build install

    # missing docs
    install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -m644 xorgproto/*.txt "${pkgdir}/usr/share/doc/${pkgname}/"
    install -m644 xorgproto/PM_spec "${pkgdir}/usr/share/doc/${pkgname}/"
    rm "${pkgdir}/usr/share/doc/${pkgname}/meson_options.txt"

    # licenses
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 xorgproto/COPYING* "${pkgdir}/usr/share/licenses/${pkgname}/"

    # cleanup
    rm -f "${pkgdir}/usr/include/X11/extensions/"{apple,windows}*
    rm -f "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-"{apple,windows}wmproto
    rm -f "${pkgdir}/usr/share/pkgconfig/"{apple,windows}wmproto.pc
}
