# Maintainer: Hal Gentz <zegentzy@protonmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-tk-gentz
pkgver=8.6.9
pkgrel=23
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('lib32-libxss' 'lib32-libxft' "lib32-tcl>=${pkgver}" 'tk' 'xorgproto')
provides=('lib32-tk')
options=('staticlibs')
source=("http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz")
groups=('gentz_custom')
sha512sums=('654d73aad006e628110057681477b958b7ec0a42287bc78259fb4b84635e7ebe757e69099d66c0c73479bc506ca66fa5baf13f13b0f403d60b2c54fd48a4a686')

exports_32() {
    export PKG_CONFIG="/usr/bin/pkg-config-32"
    export CFLAGS="$CFLAGS -m32"
    export CXXFLAGS="$CXXFLAGS -m32"
    export LDFLAGS="$LDFLAGS -m32"
}

build() {
    cd "${srcdir}/tk${pkgver}/unix"
    exports_32
    ./configure --prefix=/usr --mandir=/usr/share/man \
        --enable-threads --disable-rpath \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32 \
        --with-tcl=/usr/lib32/
    make
}

check() {
    cd "${srcdir}/tk${pkgver}/unix"
    exports_32
    make test
}

package() {
    cd "${srcdir}/tk${pkgver}/unix"

    exports_32
    make INSTALL_ROOT="${pkgdir}" install install-private-headers
    mv "${pkgdir}/usr/bin/wish${pkgver%.*}" \
        "${pkgdir}/usr/bin/wish${pkgver%.*}-32"
    ln -sf "wish${pkgver%.*}-32" "${pkgdir}/usr/bin/wish-32"
    ln -sf "libtk${pkgver%.*}.so" "${pkgdir}/usr/lib32/libtk.so"
    
    rm -rf "${pkgdir}/usr/"{share/man,include,lib}
    
    install -Dm644 license.terms \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    
    # remove buildroot traces
    sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib32#" \
        -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
        -i "${pkgdir}/usr/lib32/tkConfig.sh"
}
